//清除画面
Layer.clear(-);
//RPG地图设定开始
RPGMap.start();
//RPG地图初始化开始
initialization.start;
//地图数据设定，包括地形和图片等
addMap(r01.rmap);
//加入人物角色，参数以此为 人物index,动作action,方向direction,坐标x,坐标y,是否可控ishero
RPGCharacter.add(1,stand,right,52,19,true);
RPGCharacter.add(2,stand,down,52,13,false);
RPGCharacter.add(3,stand,down,60,15,false);
RPGCharacter.add(4,stand,up,35,40,false);
RPGCharacter.add(5,stand,left,35,7,false);
RPGCharacter.add(8,stand,down,55,25,false);
//RPG地图初始化结束
initialization.end;
//RPG地图中各函数初始化开始
function.start;
function mapR02();
	//清空画面
	Layer.clear(-);
	//读取下一地图脚本
	Load.script(script/R02.ls);
endfunction;
//函数名称为“characterclick”+人物序号的函数，功能是当点击到某人物的时候，会调用相应的函数
function characterclick2();
	if(@task1000==1);  
		RPGTalk.set(2,0,你知道lufy吗，听说那家伙除了做游戏，啥都不会。);
		RPGTalk.set(1,0,怪不得啊，哈哈哈。);
	elseif(@task1001==1);  
		RPGTalk.set(2,0,《天书残卷》交给酒店老板了吗？); 
	else;  
		RPGTalk.set(2,0,这本《天书残卷》是我前两天从酒店老板那里借来的，你能帮我还回去吗？); 
		RPGTalk.set(1,0,好啊，我正想去酒店喝点儿酒呢。);
		RPGProps.add(2);
		Var.set(task1001,1); 
		RPGMessageBox.show(得到了《仙书残卷》。请到酒店把它交给酒店老板吧。);
	endif; 
endfunction;
function characterclick3();
	if(@task1010==1);  
		RPGTalk.set(3,0,关羽的服务还好吗？);
	else;  
		RPGRunMode.set(1);
		RPGTalk.set(1,0,先来玩个测试吧，关羽向坐标（60，13）移动。);
		RPGCharacter.moveTo(2,60,13);
		RPGTalk.set(1,0,接着，关羽向左移动。);
		RPGCharacter.move(2,-4,0);
		RPGTalk.set(1,0,接着，关羽向我的下方移动。);
		RPGCharacter.moveToCharacter(2,1,0,4);
		RPGTalk.set(1,0,接下来，我和关羽一起向右。);
		RPGCharacter.move(1,4,0,0);
		RPGCharacter.move(2,4,0);
		RPGTalk.set(1,0,好，移动测试结束，下面测试动作变换。首先关羽，向上转，原地踏步一次，然后再向左转，持续原地踏步！);
		RPGCharacter.changeAction(2,move,up,0,1);
		RPGCharacter.changeAction(2,move,left,1);
		RPGTalk.set(1,0,立定！);
		RPGCharacter.changeAction(2,stand,left,1);
		RPGTalk.set(1,0,接下来，跟我互相对视！);
		RPGCharacter.changeAction(1,stand,2,1,0);
		RPGCharacter.changeAction(2,stand,1,1);
		RPGTalk.set(1,0,好，动作变换测试结束！下面来进行人物的进入地图和离开地图测试。关羽，你先给我消失。);
		RPGCharacter.remove(2);
		RPGTalk.set(1,0,关羽，进入战场，并走到我的左边来。);
		RPGCharacter.add(2,stand,down,52,13,false);
		RPGCharacter.moveToCharacter(2,1,-2,0);
		RPGTalk.set(1,0,好，本次测试结束！);
		RPGCharacter.changeAction(1,stand,3,1,0);
		RPGCharacter.changeAction(3,stand,1,1);
		RPGTalk.set(1,0,少年，你能帮我捡肥皂吗？);
		RPGTalk.set(3,0,你是在消遣我吗？);
		RPGTalk.set(1,0,是的，少年，你能帮我捡肥皂吗？);
		RPGTalk.set(3,0,你要是能打赢我，我就让那边的关羽帮你捡肥皂。);
		RPGTalk.set(1,0,那就开战吧！);
		//进入战斗，参数战斗配置文件序号
		RPGBattle.start(1);
		if(@OutcomeBattle==1);
			RPGTalk.set(3,0,竟然打败了我，那以后关羽就跟你了。);
			RPGTalk.set(2,0,猫了个咪的，关我什么事！？);
			RPGMember.add(2);
			Var.set(task1010,1); 
			RPGMessageBox.show(关羽加入队伍。);
		else;
			RPGTalk.set(3,0,你还是帮我捡肥皂吧。);
			RPGTalk.set(1,0,这......);
		endif;
		RPGRunMode.set(0);
	endif;
endfunction;
function characterclick4();
	RPGTalk.set(4,0,请不要跟我说话，我只是过来打酱油的。);
	RPGTalk.set(1,0,不跟我说话是因为看不起我吗？);
endfunction;
function characterclick5();
	RPGTalk.set(5,0,你现在进入的是由lufylegend.js制作的一个虚幻的游戏脚本世界。目前正在执行是对话脚本。);
endfunction;
function characterclick8();
	RPGTalk.set(1,0,哥们儿，你的形象不对啊。);
	RPGTalk.set(8,0,路过而已，测试一下任意大小的图片是不是能用。);
	RPGTalk.set(1,0,你能不能停下来？);
	RPGTalk.set(8,0,停不下来，这个形象，八方向的图片只找到了跑的，没找到静止的。);
	RPGTalk.set(1,0,我能说一声“靠”吗？);
endfunction;
//RPG地图中各函数初始化结束
function.end;
//RPG地图中实时判定部分
loop.start;
//添加地图坐标区域判定，参数（人物序号，起始坐标x，起始坐标y，终了坐标x，终了坐标y，触发函数名称）
RPGCharacter.atCoordinate(1,43,27,45,29,mapR02);
loop.end;
//RPG地图设定结束
RPGMap.end();
RPGItem.add(entrance,44,28);
if(@task1!=1);
	RPGProps.add(1);
	RPGProps.add(1);
	RPGProps.add(3);
	RPGProps.add(3);
	RPGProps.add(3);
	RPGProps.add(3);
	RPGProps.add(3);
	RPGProps.add(3);
	RPGProps.add(3);
	Var.set(task1,1); 
endif;
RPGMember.add(1);